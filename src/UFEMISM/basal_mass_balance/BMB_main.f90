MODULE BMB_main

  ! The main BMB model module.

! ===== Preamble =====
! ====================

  USE precisions                                             , ONLY: dp
  use UPSY_main, only: UPSY
  USE mpi_basic                                              , ONLY: par, sync
  USE call_stack_and_comp_time_tracking                  , ONLY: crash, init_routine, finalise_routine
  USE model_configuration                                    , ONLY: C
  USE parameters
  USE mesh_types                                             , ONLY: type_mesh
  use ice_model_data, only: atype_ice_model_data
  use ice_geometry_model_data, only: atype_ice_geometry_model_data
  USE ocean_model_types                                      , ONLY: type_ocean_model
  USE reference_geometry_types                               , ONLY: type_reference_geometry
  USE BMB_model_types                                        , ONLY: type_BMB_model
  use climate_model_types, only: type_climate_model
  use climate_retreat_mask, only: retreat_mask_threshold, retreat_mask_BMB_shelf
  USE laddie_model_types                                     , ONLY: type_laddie_model
  USE laddie_forcing_types                                   , ONLY: type_laddie_forcing
  USE BMB_idealised                                          , ONLY: initialise_BMB_model_idealised, run_BMB_model_idealised
  USE BMB_prescribed                                         , ONLY: initialise_BMB_model_prescribed, run_BMB_model_prescribed
  USE BMB_parameterised                                      , ONLY: initialise_BMB_model_parameterised, run_BMB_model_parameterised
  USE BMB_laddie                                             , ONLY: initialise_BMB_model_laddie, run_BMB_model_laddie, remap_BMB_model_laddie
  use BMB_inverted, only: initialise_BMB_model_inverted, run_BMB_model_inverted
  use LADDIE_main_model, only: initialise_laddie_model, run_laddie_model
  use laddie_main_utils, only: remap_laddie_model
  use laddie_utilities, only: allocate_laddie_forcing
  use laddie_forcing_main, only: calculate_coriolis_parameter
  use laddie_hydrology, only: initialise_transects_SGD
  USE reallocate_mod                                         , ONLY: reallocate_bounds
  use ice_geometry_basics, only: is_floating
  USE mesh_utilities                                         , ONLY: extrapolate_Gaussian
  use netcdf_io_main
  use checksum_mod, only: checksum
  use mpi_distributed_shared_memory, only: reallocate_dist_shared
  use thermodynamics_utilities                                , ONLY: calc_grounded_basal_melt_rates_from_temp
  use checksum_mod, only: checksum

  IMPLICIT NONE

CONTAINS

! ===== Main routines =====
! =========================

  SUBROUTINE run_BMB_model( mesh, ice, geom, ocean, refgeo, BMB, region_name, time, climate, is_initial)
    ! Calculate the basal mass balance

    ! In/output variables:
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    class(atype_ice_model_data),            INTENT(IN)    :: ice
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    TYPE(type_ocean_model),                 INTENT(IN)    :: ocean
    TYPE(type_reference_geometry),          INTENT(IN)    :: refgeo
    TYPE(type_BMB_model),                   INTENT(INOUT) :: BMB
    CHARACTER(LEN=3),                       INTENT(IN)    :: region_name
    REAL(dp),                               INTENT(IN)    :: time
    logical,                                intent(in)    :: is_initial
    type(type_climate_model),               intent(in)    :: climate

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'run_BMB_model'
    CHARACTER(LEN=256)                                    :: choice_BMB_model
    CHARACTER(LEN=256)                                    :: choice_BMB_model_ROI
    INTEGER                                               :: vi
    logical                                               :: do_long_initialisation

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Determine whether long initialisation is needed
    if (C%choice_laddie_model_initialisation == 'uniform') then
      do_long_initialisation = is_initial
    else
      do_long_initialisation = .false.
    end if

    ! Determine which BMB model to run for this region
    SELECT CASE (region_name)
      CASE ('NAM')
        choice_BMB_model      = C%choice_BMB_model_NAM
        choice_BMB_model_ROI  = C%choice_BMB_model_NAM_ROI
      CASE ('EAS')
        choice_BMB_model      = C%choice_BMB_model_EAS
        choice_BMB_model_ROI  = C%choice_BMB_model_EAS_ROI
      CASE ('GRL')
        choice_BMB_model      = C%choice_BMB_model_GRL
        choice_BMB_model_ROI  = C%choice_BMB_model_GRL_ROI
      CASE ('ANT')
        choice_BMB_model      = C%choice_BMB_model_ANT
        choice_BMB_model_ROI  = C%choice_BMB_model_ANT_ROI
      CASE DEFAULT
        CALL crash('unknown region_name "' // region_name // '"')
    END SELECT

    ! Check if we need to calculate a new BMB
    IF (C%do_asynchronous_BMB) THEN
      ! Asynchronous coupling: do not calculate a new BMB in
      ! every model loop, but only at its own separate time step

      ! Check if this is the next BMB time step
      IF (time == BMB%t_next) THEN
        ! Go on to calculate a new BMB
        BMB%t_next = time + C%dt_BMB
      ELSEIF (time > BMB%t_next) THEN
        ! This should not be possible
        CALL crash('overshot the BMB time step')
      ELSE
        ! It is not yet time to calculate a new BMB

        ! Apply subgrid scheme of old BMB to new mask
        SELECT CASE (choice_BMB_model)
          CASE ('inverted')
            ! No need to do anything
          CASE ('prescribed_fixed')
            ! No need to do anything
          CASE DEFAULT
            CALL apply_BMB_subgrid_scheme( mesh, geom, BMB)
        END SELECT

        ! Prescribe shelf melt where the retreat mask is active
        if (C%do_use_ISMIP_future_shelf_collapse_forcing .and. C%shelf_collapse_type == 'BMB') &
          call apply_retreat_mask_BMB( mesh, geom, climate, BMB, C%do_BMB_transition_phase)

        CALL finalise_routine( routine_name)
        RETURN
      END IF

    ELSE ! IF (C%do_asynchronous_BMB) THEN
      ! Synchronous coupling: calculate a new BMB in every model loop
      BMB%t_next = time + C%dt_BMB
    END IF

    ! Compute grounded ice mass balance
    SELECT CASE (C%choice_BMB_grounded)
      CASE ('from_temperature')
        call calc_grounded_basal_melt_rates_from_temp( mesh, ice, geom, BMB)
      CASE ('none')
        ! Do nothing
      CASE DEFAULT
        ! Do nothing
    END SELECT

    ! Re-initialise BMB model if needed, only for LADDIE
    if (time > BMB%t_next_reinit) then
      select case (choice_BMB_model)
        case default
          !No need to do anything
        case ('laddie')
          call update_laddie_forcing( mesh, ice, geom, ocean, BMB%forcing, region_name)
          call initialise_laddie_model( mesh, BMB%laddie, BMB%forcing, .false.)
          call run_laddie_model( mesh, BMB%laddie, BMB%forcing, time, .true., .false.)
          BMB%t_next_reinit = BMB%t_next_reinit + C%dt_BMB_reinit
      end select
    end if

    ! Run the chosen BMB model
    SELECT CASE (choice_BMB_model)
      CASE ('uniform')
        BMB%BMB_shelf = 0._dp
        if (time > C%uniform_BMB_t_start) then
          DO vi = mesh%vi1, mesh%vi2
            IF (geom%mask_floating_ice( vi) .OR. geom%mask_icefree_ocean( vi) .OR. geom%mask_gl_gr( vi)) THEN
              BMB%BMB_shelf( vi) = C%uniform_BMB
            END IF
          END DO
        end if
      CASE ('prescribed')
        CALL run_BMB_model_prescribed( mesh, BMB, region_name, time)
      CASE ('prescribed_fixed')
        ! No need to do anything
      CASE ('idealised')
        if (time > C%uniform_BMB_t_start) then
          CALL run_BMB_model_idealised( mesh, ice, geom, BMB, time)
        end if
      CASE ('parameterised')
        if (time > C%uniform_BMB_t_start) then
          CALL run_BMB_model_parameterised( mesh, geom, ocean, BMB)
        end if
      CASE ('inverted')
        CALL run_BMB_model_inverted( mesh, ice, geom, BMB%inv, time)
        BMB%BMB = BMB%inv%BMB
        ! Separate into BMB_sheet and BMB_shelf for scalar diagnostics
        do vi = mesh%vi1, mesh%vi2
          if (geom%mask_floating_ice( vi)) then
            BMB%BMB_shelf( vi) = BMB%BMB( vi)
          else
            BMB%BMB_shelf( vi) = 0._dp
          end if
          if (geom%mask_grounded_ice( vi)) then
            BMB%BMB_sheet( vi) = BMB%BMB( vi)
          else
            BMB%BMB_sheet( vi) = 0._dp
          end if
        end do
      CASE ('laddie_py')
        CALL run_BMB_model_laddie( mesh, ice, geom, BMB, time, .FALSE.)
      case ('laddie')
        call update_laddie_forcing( mesh, ice, geom, ocean, BMB%forcing, region_name)
        call run_laddie_model( mesh, BMB%laddie, BMB%forcing, time, do_long_initialisation, .false.)
        BMB%BMB_shelf = 0._dp
        do vi = mesh%vi1, mesh%vi2
          BMB%BMB_shelf( vi) = -BMB%laddie%melt( vi) * sec_per_year
        end do
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model "' // TRIM( choice_BMB_model) // '"')
    END SELECT

    ! Check hybrid_ROI_BMB
    SELECT CASE (choice_BMB_model_ROI)
      CASE ('identical_to_choice_BMB_model')
        ! No need to do anything
      CASE ('uniform')
        ! Update BMB only for cells in ROI
        DO vi = mesh%vi1, mesh%vi2
          IF (ice%mask_ROI(vi) > 0) THEN
            IF (geom%mask_floating_ice( vi) .OR. geom%mask_icefree_ocean( vi) .OR. geom%mask_gl_gr( vi)) THEN
              BMB%BMB_shelf( vi) = C%uniform_BMB_ROI
            END IF
          END IF
        END DO
        CALL apply_BMB_subgrid_scheme_ROI( mesh, ice, geom, BMB)
      CASE ('laddie_py')
        ! run_BMB_model_laddie and read BMB values only for region of interest
        CALL run_BMB_model_laddie( mesh, ice, geom, BMB, time, .TRUE.)
        CALL apply_BMB_subgrid_scheme_ROI( mesh, ice, geom, BMB)
      CASE ('prescribed', 'prescribed_fixed', 'idealised', 'parameterised', 'inverted', 'laddie')
        CALL crash('this BMB_model "' // TRIM( choice_BMB_model_ROI) // '" is not implemented for hybrid-BMB in ROI yet')
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model_ROI "' // TRIM( choice_BMB_model_ROI) // '"')
    END SELECT

    ! Apply subgrid scheme of old BMB to new mask
    SELECT CASE (choice_BMB_model)
      CASE ('inverted')
        ! No need to do anything
      CASE ('prescribed_fixed')
        ! No need to do anything
      CASE DEFAULT
        CALL apply_BMB_subgrid_scheme( mesh, geom, BMB)
    END SELECT

    ! Prescribe shelf melt where the retreat mask is active
    if (C%do_use_ISMIP_future_shelf_collapse_forcing .and. C%shelf_collapse_type == 'BMB') &
      call apply_retreat_mask_BMB( mesh, geom, climate, BMB, .false.)

    ! save BMB in BMB_modelled if applying transition phase
    IF (C%do_BMB_transition_phase) THEN
      DO vi = mesh%vi1, mesh%vi2
        BMB%BMB_modelled( vi) = BMB%BMB( vi)
      END DO
    END IF

    ! Apply limits
    BMB%BMB = max( -C%BMB_maximum_allowed_melt_rate, min( C%BMB_maximum_allowed_refreezing_rate, BMB%BMB ))

    call checksum( mesh%pai_V, BMB%BMB                 , 'BMB%BMB')
    call checksum( mesh%pai_V, BMB%BMB_shelf           , 'BMB%BMB_shelf')
    call checksum( mesh%pai_V, BMB%BMB_inv             , 'BMB%BMB_inv')
    call checksum( mesh%pai_V, BMB%BMB_transition_phase, 'BMB%BMB_transition_phase')
    call checksum( mesh%pai_V, BMB%BMB_modelled        , 'BMB%BMB_modelled')

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE run_BMB_model

  SUBROUTINE initialise_BMB_model( mesh, ice, geom, ocean, BMB, refgeo_PD, refgeo_init, region_name)
    ! Initialise the BMB model

    ! In- and output variables
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    class(atype_ice_model_data),            INTENT(IN)    :: ice
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    TYPE(type_ocean_model),                 INTENT(IN)    :: ocean
    TYPE(type_BMB_model),                   INTENT(OUT)   :: BMB
    type(type_reference_geometry),          intent(in   ) :: refgeo_PD, refgeo_init
    CHARACTER(LEN=3),                       INTENT(IN)    :: region_name

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'initialise_BMB_model'
    CHARACTER(LEN=256)                                    :: choice_BMB_model
    CHARACTER(LEN=256)                                    :: choice_BMB_model_ROI
    integer                                               :: vi

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Print to terminal
    IF (par%primary)  WRITE(*,"(A)") '   Initialising basal mass balance model...'

    ! Determine which BMB model to initialise for this region
    SELECT CASE (region_name)
      CASE ('NAM')
        choice_BMB_model      = C%choice_BMB_model_NAM
        choice_BMB_model_ROI  = C%choice_BMB_model_NAM_ROI
      CASE ('EAS')
        choice_BMB_model      = C%choice_BMB_model_EAS
        choice_BMB_model_ROI  = C%choice_BMB_model_EAS_ROI
      CASE ('GRL')
        choice_BMB_model      = C%choice_BMB_model_GRL
        choice_BMB_model_ROI  = C%choice_BMB_model_GRL_ROI
      CASE ('ANT')
        choice_BMB_model      = C%choice_BMB_model_ANT
        choice_BMB_model_ROI  = C%choice_BMB_model_ANT_ROI
      CASE DEFAULT
        CALL crash('unknown region_name "' // region_name // '"')
    END SELECT

    ! These models set the applied BMB directly instead of recomputing it from BMB_shelf,
    ! so a prescribed retreat melt could not be withdrawn when the mask becomes inactive
    if (C%do_use_ISMIP_future_shelf_collapse_forcing .and. C%shelf_collapse_type == 'BMB') then
      if (choice_BMB_model == 'inverted' .or. choice_BMB_model == 'prescribed_fixed') &
        call crash('shelf_collapse_type = "BMB" cannot be combined with choice_BMB_model "' // &
          trim( choice_BMB_model) // '"')
    end if

    ! Allocate memory for main variables
    ALLOCATE( BMB%BMB( mesh%vi1:mesh%vi2))
    BMB%BMB = 0._dp

    ! Allocate shelf BMB
    ALLOCATE( BMB%BMB_shelf( mesh%vi1:mesh%vi2))
    BMB%BMB_shelf = 0._dp

    ! Allocate sheet BMB
    ALLOCATE( BMB%BMB_sheet( mesh%vi1:mesh%vi2))
    BMB%BMB_sheet = 0._dp

    ! Allocate inverted BMB
    ALLOCATE( BMB%BMB_inv( mesh%vi1:mesh%vi2))
    BMB%BMB_inv = 0._dp

    ! Allocate transition phase BMB
    ALLOCATE( BMB%BMB_transition_phase( mesh%vi1:mesh%vi2))
    BMB%BMB_transition_phase = 0._dp

    ! Allocate modelled BMB
    ALLOCATE( BMB%BMB_modelled( mesh%vi1:mesh%vi2))
    BMB%BMB_modelled = 0._dp

    ! Allocate prescribed retreat melt diagnostics
    ALLOCATE( BMB%dBMB_fl_retreat( mesh%vi1:mesh%vi2))
    BMB%dBMB_fl_retreat = 0._dp
    ALLOCATE( BMB%mask_retreat_BMB( mesh%vi1:mesh%vi2))
    BMB%mask_retreat_BMB = .false.

    ! Set time of next calculation to start time
    BMB%t_next = C%start_time_of_run

    ! Set time of next reinitialisation to avoid double initialisation at start
    BMB%t_next_reinit = C%start_time_of_run + C%dt_BMB_reinit

    ! Compute grounded ice mass balance
    SELECT CASE (C%choice_BMB_grounded)
      CASE ('from_temperature')
        call calc_grounded_basal_melt_rates_from_temp( mesh, ice, geom, BMB)
      CASE ('none')
        ! Do nothing
      CASE DEFAULT
        ! Do nothing
    END SELECT

    ! Determine which BMB model to initialise
    SELECT CASE (choice_BMB_model)
      CASE ('uniform')
        ! No need to do anything
      CASE ('prescribed')
        CALL initialise_BMB_model_prescribed( mesh, BMB, region_name)
      CASE ('prescribed_fixed')
        CALL initialise_BMB_model_prescribed( mesh, BMB, region_name)
        CALL apply_BMB_subgrid_scheme( mesh, geom, BMB)
      CASE ('idealised')
        CALL initialise_BMB_model_idealised( mesh, BMB)
      CASE ('parameterised')
        CALL initialise_BMB_model_parameterised( mesh, BMB)
      CASE ('inverted')
        call initialise_BMB_model_inverted( mesh, BMB%inv, refgeo_PD, refgeo_init)
      CASE ('laddie_py')
        CALL initialise_BMB_model_laddie( mesh, BMB)
      CASE ('laddie')
        call allocate_laddie_forcing( mesh, BMB%forcing)
        call update_laddie_forcing( mesh, ice, geom, ocean, BMB%forcing, region_name)
        call initialise_transects_SGD( mesh, BMB%forcing)
        call initialise_laddie_model( mesh, BMB%laddie, BMB%forcing, .false.)
        BMB%BMB_shelf = 0._dp
        do vi = mesh%vi1, mesh%vi2
          BMB%BMB_shelf( vi) = -BMB%laddie%melt( vi) * sec_per_year
        end do
        call apply_BMB_subgrid_scheme( mesh, geom, BMB)
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model "' // TRIM( choice_BMB_model) // '"')
    END SELECT

    ! Check hybrid_ROI_BMB
    SELECT CASE (choice_BMB_model_ROI)
      CASE ('identical_to_choice_BMB_model')
        ! No need to do anything
      CASE ('uniform')
        ! No need to do anything
      CASE ('laddie_py')
         CALL initialise_BMB_model_laddie( mesh, BMB)
      CASE ('prescribed', 'prescribed_fixed', 'idealised', 'parameterised', 'inverted', 'laddie')
        CALL crash('this BMB_model "' // TRIM( choice_BMB_model_ROI) // '" is not implemented for hybrid-BMB in ROI yet')
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model_ROI "' // TRIM( choice_BMB_model_ROI) // '"')
    END SELECT

    call checksum( mesh%pai_V, BMB%BMB                 , 'BMB%BMB')
    call checksum( mesh%pai_V, BMB%BMB_shelf           , 'BMB%BMB_shelf')
    call checksum( mesh%pai_V, BMB%BMB_sheet           , 'BMB%BMB_sheet')
    call checksum( mesh%pai_V, BMB%BMB_inv             , 'BMB%BMB_inv')
    call checksum( mesh%pai_V, BMB%BMB_transition_phase, 'BMB%BMB_transition_phase')
    call checksum( mesh%pai_V, BMB%BMB_modelled        , 'BMB%BMB_modelled')

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE initialise_BMB_model

  SUBROUTINE write_to_restart_file_BMB_model( mesh, BMB, region_name, time)
    ! Write to the restart file for the BMB model

    ! In/output variables:
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    TYPE(type_BMB_model),                   INTENT(IN)    :: BMB
    CHARACTER(LEN=3),                       INTENT(IN)    :: region_name
    REAL(dp),                               INTENT(IN)    :: time

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'write_to_restart_file_BMB_model'
    CHARACTER(LEN=256)                                    :: choice_BMB_model

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Determine which BMB model to initialise for this region
    SELECT CASE (region_name)
      CASE ('NAM')
        choice_BMB_model = C%choice_BMB_model_NAM
      CASE ('EAS')
        choice_BMB_model = C%choice_BMB_model_EAS
      CASE ('GRL')
        choice_BMB_model = C%choice_BMB_model_GRL
      CASE ('ANT')
        choice_BMB_model = C%choice_BMB_model_ANT
      CASE DEFAULT
        CALL crash('unknown region_name "' // region_name // '"')
    END SELECT

    ! Write to the restart file of the chosen BMB model
    SELECT CASE (choice_BMB_model)
      CASE ('uniform')
        ! No need to do anything
      CASE ('prescribed')
        ! No need to do anything
      CASE ('prescribed_fixed')
        ! No need to do anything
      CASE ('idealised')
        ! No need to do anything
      CASE ('parameterised')
        ! No need to do anything
      CASE ('inverted')
        CALL write_to_restart_file_BMB_model_region( mesh, BMB, region_name, time)
      CASE ('laddie_py')
        ! No need to do anything
      CASE ('laddie')
        CALL write_to_restart_file_BMB_laddie_region( mesh, BMB, region_name, time)
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model "' // TRIM( choice_BMB_model) // '"')
    END SELECT

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE write_to_restart_file_BMB_model

  SUBROUTINE write_to_restart_file_BMB_model_region( mesh, BMB, region_name, time)
    ! Write to the restart NetCDF file for the BMB model

    ! In/output variables:
    TYPE(type_mesh),          INTENT(IN) :: mesh
    TYPE(type_BMB_model),     INTENT(IN) :: BMB
    CHARACTER(LEN=3),         INTENT(IN) :: region_name
    REAL(dp),                 INTENT(IN) :: time

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER        :: routine_name = 'write_to_restart_file_BMB_model_region'
    INTEGER                              :: ncid

    ! Add routine to path
    CALL init_routine( routine_name)

    ! If no NetCDF output should be created, do nothing
    IF (.NOT. C%do_create_netcdf_output) THEN
      CALL finalise_routine( routine_name)
      RETURN
    END IF

    ! Print to terminal
    IF (par%primary) WRITE(0,'(A)') '   Writing to BMB restart file "' // &
      UPSY%stru%colour_string( TRIM( BMB%restart_filename), 'light blue') // '"...'

    ! Open the NetCDF file
    CALL open_existing_netcdf_file_for_writing( BMB%restart_filename, ncid)

    ! Write the time to the file
    CALL write_time_to_file( BMB%restart_filename, ncid, time)

    ! ! Write the BMB fields to the file
    CALL write_to_field_multopt_mesh_dp_2D( mesh, BMB%restart_filename, ncid, 'BMB', BMB%BMB)

    ! Close the file
    CALL close_netcdf_file( ncid)

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE write_to_restart_file_BMB_model_region

  SUBROUTINE write_to_restart_file_BMB_laddie_region( mesh, BMB, region_name, time)
    ! Write to the restart NetCDF file specific for LADDIE

    ! In/output variables:
    TYPE(type_mesh),          INTENT(IN) :: mesh
    TYPE(type_BMB_model),     INTENT(IN) :: BMB
    CHARACTER(LEN=3),         INTENT(IN) :: region_name
    REAL(dp),                 INTENT(IN) :: time

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER        :: routine_name = 'write_to_restart_file_BMB_laddie_region'
    INTEGER                              :: ncid

    ! Add routine to path
    CALL init_routine( routine_name)

    ! If no NetCDF output should be created, do nothing
    IF (.NOT. C%do_create_netcdf_output) THEN
      CALL finalise_routine( routine_name)
      RETURN
    END IF

    ! Print to terminal
    IF (par%primary) WRITE(0,'(A)') '   Writing to BMB restart file "' // &
      UPSY%stru%colour_string( TRIM( BMB%restart_filename), 'light blue') // '"...'

    ! Open the NetCDF file
    CALL open_existing_netcdf_file_for_writing( BMB%restart_filename, ncid)

    ! Write the time to the file
    CALL write_time_to_file( BMB%restart_filename, ncid, time)

    ! ! Write the LADDIE fields to the file
    CALL write_to_field_multopt_mesh_dp_2D( mesh, BMB%restart_filename, ncid, 'H_lad', BMB%laddie%now%H)
    CALL write_to_field_multopt_mesh_dp_2D_b( mesh, BMB%restart_filename, ncid, 'U_lad', BMB%laddie%now%U)
    CALL write_to_field_multopt_mesh_dp_2D_b( mesh, BMB%restart_filename, ncid, 'V_lad', BMB%laddie%now%V)
    CALL write_to_field_multopt_mesh_dp_2D( mesh, BMB%restart_filename, ncid, 'T_lad', BMB%laddie%now%T)
    CALL write_to_field_multopt_mesh_dp_2D( mesh, BMB%restart_filename, ncid, 'S_lad', BMB%laddie%now%S)

    ! Close the file
    CALL close_netcdf_file( ncid)

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE write_to_restart_file_BMB_laddie_region

  SUBROUTINE create_restart_file_BMB_model( mesh, BMB, region_name)
    ! Create the restart file for the BMB model

    ! In/output variables:
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    TYPE(type_BMB_model),                   INTENT(INOUT) :: BMB
    CHARACTER(LEN=3),                       INTENT(IN)    :: region_name

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'create_restart_file_BMB_model'
    CHARACTER(LEN=256)                                    :: choice_BMB_model

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Determine which BMB model to initialise for this region
    SELECT CASE (region_name)
      CASE ('NAM')
        choice_BMB_model = C%choice_BMB_model_NAM
      CASE ('EAS')
        choice_BMB_model = C%choice_BMB_model_EAS
      CASE ('GRL')
        choice_BMB_model = C%choice_BMB_model_GRL
      CASE ('ANT')
        choice_BMB_model = C%choice_BMB_model_ANT
      CASE DEFAULT
        CALL crash('unknown region_name "' // region_name // '"')
    END SELECT

    ! Create the restart file of the chosen BMB model
    SELECT CASE (choice_BMB_model)
      CASE ('uniform')
        ! No need to do anything
      CASE ('prescribed')
        ! No need to do anything
      CASE ('prescribed_fixed')
        ! No need to do anything
      CASE ('idealised')
        ! No need to do anything
      CASE ('parameterised')
        ! No need to do anything
      CASE ('inverted')
        CALL create_restart_file_BMB_model_region( mesh, BMB, region_name)
      CASE ('laddie_py')
        ! No need to do anything
      CASE ('laddie')
        CALL create_restart_file_BMB_laddie_region( mesh, BMB, region_name)
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model "' // TRIM( choice_BMB_model) // '"')
    END SELECT

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE create_restart_file_BMB_model

  SUBROUTINE create_restart_file_BMB_model_region( mesh, BMB, region_name)
    ! Create a restart NetCDF file for the BMB submodel
    ! Includes generation of the procedural filename (e.g. "restart_BMB_00001.nc")

    ! In/output variables:
    TYPE(type_mesh),          INTENT(IN)    :: mesh
    TYPE(type_BMB_model),     INTENT(INOUT) :: BMB
    CHARACTER(LEN=3),         INTENT(IN)    :: region_name

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER           :: routine_name = 'create_restart_file_BMB_model_region'
    CHARACTER(LEN=256)                      :: filename_base
    INTEGER                                 :: ncid

    ! Add routine to path
    CALL init_routine( routine_name)

    ! If no NetCDF output should be created, do nothing
    IF (.NOT. C%do_create_netcdf_output) THEN
      CALL finalise_routine( routine_name)
      RETURN
    END IF

    ! Set the filename
    filename_base = TRIM( C%output_dir) // 'restart_BMB_' // region_name
    CALL generate_filename_XXXXXdotnc( filename_base, BMB%restart_filename)

    ! Print to terminal
    IF (par%primary) WRITE(0,'(A)') '   Creating BMB model restart file "' // &
      UPSY%stru%colour_string( TRIM( BMB%restart_filename), 'light blue') // '"...'

    ! Create the NetCDF file
    CALL create_new_netcdf_file_for_writing( BMB%restart_filename, ncid)

    ! Set up the mesh in the file
    CALL setup_mesh_in_netcdf_file( BMB%restart_filename, ncid, mesh)

    ! Add a time dimension to the file
    CALL add_time_dimension_to_file( BMB%restart_filename, ncid)

    ! Add the data fields to the file
    CALL add_field_mesh_dp_2D( BMB%restart_filename, ncid, 'BMB', long_name = 'Basal mass balance', units = 'm/yr')

    ! Close the file
    CALL close_netcdf_file( ncid)

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE create_restart_file_BMB_model_region

  SUBROUTINE create_restart_file_BMB_laddie_region( mesh, BMB, region_name)
    ! Create a restart NetCDF file specific for LADDIE
    ! Includes generation of the procedural filename (e.g. "restart_BMB_00001.nc")

    ! In/output variables:
    TYPE(type_mesh),          INTENT(IN)    :: mesh
    TYPE(type_BMB_model),     INTENT(INOUT) :: BMB
    CHARACTER(LEN=3),         INTENT(IN)    :: region_name

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER           :: routine_name = 'create_restart_file_BMB_laddie_region'
    CHARACTER(LEN=256)                      :: filename_base
    INTEGER                                 :: ncid

    ! Add routine to path
    CALL init_routine( routine_name)

    ! If no NetCDF output should be created, do nothing
    IF (.NOT. C%do_create_netcdf_output) THEN
      CALL finalise_routine( routine_name)
      RETURN
    END IF

    ! Set the filename
    filename_base = TRIM( C%output_dir) // 'restart_BMB_' // region_name
    CALL generate_filename_XXXXXdotnc( filename_base, BMB%restart_filename)

    ! Print to terminal
    IF (par%primary) WRITE(0,'(A)') '   Creating BMB model restart file "' // &
      UPSY%stru%colour_string( TRIM( BMB%restart_filename), 'light blue') // '"...'

    ! Create the NetCDF file
    CALL create_new_netcdf_file_for_writing( BMB%restart_filename, ncid)

    ! Set up the mesh in the file
    CALL setup_mesh_in_netcdf_file( BMB%restart_filename, ncid, mesh)

    ! Add a time dimension to the file
    CALL add_time_dimension_to_file( BMB%restart_filename, ncid)

    ! Add the data fields to the file
    CALL add_field_mesh_dp_2D(   BMB%restart_filename, ncid, 'H_lad', long_name = 'Laddie layer thickness', units = 'm')
    CALL add_field_mesh_dp_2D_b( BMB%restart_filename, ncid, 'U_lad', long_name = 'Laddie x-velocity', units = 'm/s')
    CALL add_field_mesh_dp_2D_b( BMB%restart_filename, ncid, 'V_lad', long_name = 'Laddie y-velocity', units = 'm/s')
    CALL add_field_mesh_dp_2D(   BMB%restart_filename, ncid, 'T_lad', long_name = 'Laddie temperature', units = 'degC')
    CALL add_field_mesh_dp_2D(   BMB%restart_filename, ncid, 'S_lad', long_name = 'Laddie salinity', units = 'psu')

    ! Close the file
    CALL close_netcdf_file( ncid)

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE create_restart_file_BMB_laddie_region

  SUBROUTINE remap_BMB_model( mesh_old, mesh_new, ice, geom, ocean, BMB, region_name, time, climate)
    ! Remap the BMB model

    ! In- and output variables
    TYPE(type_mesh),                        INTENT(IN)    :: mesh_old
    TYPE(type_mesh),                        INTENT(IN)    :: mesh_new
    class(atype_ice_model_data),            INTENT(IN)    :: ice
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    TYPE(type_ocean_model),                 INTENT(IN)    :: ocean
    TYPE(type_BMB_model),                   INTENT(INOUT) :: BMB
    CHARACTER(LEN=3),                       INTENT(IN)    :: region_name
    REAL(dp),                               INTENT(IN)    :: time
    type(type_climate_model),               intent(in   ) :: climate

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'remap_BMB_model'
    CHARACTER(LEN=256)                                    :: choice_BMB_model
    integer                                               :: vi

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Print to terminal
    IF (par%primary)  WRITE(*,"(A)") '    Remapping basal mass balance model data to the new mesh...'

    ! Determine which BMB model to initialise for this region
    SELECT CASE (region_name)
      CASE ('NAM')
        choice_BMB_model = C%choice_BMB_model_NAM
      CASE ('EAS')
        choice_BMB_model = C%choice_BMB_model_EAS
      CASE ('GRL')
        choice_BMB_model = C%choice_BMB_model_GRL
      CASE ('ANT')
        choice_BMB_model = C%choice_BMB_model_ANT
      CASE DEFAULT
        CALL crash('unknown region_name "' // region_name // '"')
    END SELECT

    ! Reallocate memory for main variables
    CALL reallocate_bounds( BMB%BMB, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%BMB_shelf, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%BMB_sheet, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%BMB_inv, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%BMB_transition_phase, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%BMB_modelled, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%dBMB_fl_retreat, mesh_new%vi1, mesh_new%vi2)
    CALL reallocate_bounds( BMB%mask_retreat_BMB, mesh_new%vi1, mesh_new%vi2)

    ! Compute grounded ice mass balance on the new mesh
    SELECT CASE (C%choice_BMB_grounded)
      CASE ('from_temperature')
        call calc_grounded_basal_melt_rates_from_temp( mesh_new, ice, geom, BMB)
      CASE ('none')
        ! Do nothing
      CASE DEFAULT
        ! Do nothing
    END SELECT

    ! Determine which BMB model to initialise
    SELECT CASE (choice_BMB_model)
      CASE ('uniform')
        ! No need to do anything
      CASE ('prescribed')
        CALL initialise_BMB_model_prescribed( mesh_new, BMB, region_name)
      CASE ('prescribed_fixed')
        CALL initialise_BMB_model_prescribed( mesh_new, BMB, region_name)
        CALL apply_BMB_subgrid_scheme( mesh_new, geom, BMB)
      CASE ('idealised')
        ! No need to do anything
      CASE ('parameterised')
        ! we only need to run the BMB model again, considering the ocean model is remapped just before a call to this function
        CALL run_BMB_model_parameterised( mesh_new, geom, ocean, BMB)
      CASE ('inverted')
        ! No need to do anything
      CASE ('laddie_py')
        CALL remap_BMB_model_laddie( mesh_new, BMB)
      CASE ('laddie')
        call remap_laddie_forcing( mesh_old, mesh_new, BMB%forcing)
        call update_laddie_forcing( mesh_new, ice, geom, ocean, BMB%forcing, region_name)
        call remap_laddie_model( mesh_old, mesh_new, BMB%laddie, BMB%forcing, time)
        call run_laddie_model( mesh_new, BMB%laddie, BMB%forcing, time, .false., .false.)
        BMB%BMB_shelf = 0._dp
        do vi = mesh_new%vi1, mesh_new%vi2
          BMB%BMB_shelf( vi) = -BMB%laddie%melt( vi) * sec_per_year
        end do
        call apply_BMB_subgrid_scheme( mesh_new, geom, BMB)
      CASE DEFAULT
        CALL crash('unknown choice_BMB_model "' // TRIM( choice_BMB_model) // '"')
    END SELECT

    ! Prescribe shelf melt where the (already remapped) retreat mask is active, so that it
    ! also applies in the first ice-dynamics step on the new mesh
    if (C%do_use_ISMIP_future_shelf_collapse_forcing .and. C%shelf_collapse_type == 'BMB') &
      call apply_retreat_mask_BMB( mesh_new, geom, climate, BMB, C%do_BMB_transition_phase)

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE remap_BMB_model

! ===== Utilities =====
! =====================

  SUBROUTINE apply_BMB_subgrid_scheme( mesh, geom, BMB)
    ! Apply selected scheme for sub-grid shelf melt
    ! (see Leguy et al. 2021 for explanations of the three schemes)

    ! In- and output variables
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    TYPE(type_BMB_model),                   INTENT(INOUT) :: BMB

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'apply_BMB_subgrid_scheme'
    CHARACTER(LEN=256)                                    :: choice_BMB_subgrid
    INTEGER                                               :: vi

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Note: apply extrapolation_FCMP_to_PMP to non-laddie BMB models before applying sub-grid schemes

    DO vi = mesh%vi1, mesh%vi2
      ! Different sub-grid schemes for sub-shelf melt
      CALL compute_subgrid_BMB( geom, BMB, vi)
    END DO
    CALL sync

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE apply_BMB_subgrid_scheme

  subroutine apply_retreat_mask_BMB( mesh, geom, climate, BMB, do_update_modelled)
    ! Prescribe shelf melt where the retreat mask is active
    !
    ! Only vertices where the mask is active and the sub-grid scheme assigns a floating
    ! weight are changed. There, the applied BMB is recomputed from the prescribed shelf
    ! melt and the sheet BMB with the selected sub-grid scheme, and limited to the
    ! configured maximum rates, at every call (also between asynchronous BMB updates).
    ! BMB_shelf keeps the value of the BMB model, so the modelled melt returns wherever
    ! the mask becomes inactive again. dBMB_fl_retreat holds the resulting change of the
    ! floating-ice BMB relative to BMB_shelf, so that the floating-ice diagnostics include
    ! the prescribed melt; the grounded part stays attributed to BMB_sheet.

    ! In- and output variables
    type(type_mesh),                        intent(in   ) :: mesh
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    type(type_climate_model),               intent(in   ) :: climate
    type(type_BMB_model),                   intent(inout) :: BMB
    logical,                                intent(in   ) :: do_update_modelled

    ! Local variables:
    character(len=256), parameter                         :: routine_name = 'apply_retreat_mask_BMB'
    integer                                               :: vi
    real(dp)                                              :: w_fl, w_gr
    logical                                               :: was_applied

    ! Add routine to path
    call init_routine( routine_name)

    do vi = mesh%vi1, mesh%vi2

      was_applied = BMB%mask_retreat_BMB( vi)
      BMB%mask_retreat_BMB( vi) = .false.
      BMB%dBMB_fl_retreat ( vi) = 0._dp

      if (climate%retreat%mask( vi) > retreat_mask_threshold) then
        call calc_subgrid_BMB_weights( geom, vi, w_fl, w_gr)
        if (w_fl > 0._dp) then
          BMB%BMB( vi) = w_fl * retreat_mask_BMB_shelf + w_gr * BMB%BMB_sheet( vi)
          BMB%BMB( vi) = max( -C%BMB_maximum_allowed_melt_rate, min( C%BMB_maximum_allowed_refreezing_rate, BMB%BMB( vi)))
          BMB%dBMB_fl_retreat ( vi) = (BMB%BMB( vi) - w_gr * BMB%BMB_sheet( vi)) - w_fl * BMB%BMB_shelf( vi)
          BMB%mask_retreat_BMB( vi) = .true.
        end if
      end if

      ! Keep the transition-phase record consistent where the prescribed melt starts or stops
      if (do_update_modelled .and. (was_applied .or. BMB%mask_retreat_BMB( vi))) &
        BMB%BMB_modelled( vi) = BMB%BMB( vi)

    end do

    ! Finalise routine path
    call finalise_routine( routine_name)

  end subroutine apply_retreat_mask_BMB

  subroutine calc_subgrid_BMB_weights( geom, vi, w_fl, w_gr)
    ! Weights of BMB_shelf and BMB_sheet in the selected sub-grid scheme
    ! (consistent with compute_subgrid_BMB and the BMB diagnostics)

    ! In/output variables
    class(atype_ice_geometry_model_data), intent(in   ) :: geom
    integer,                              intent(in   ) :: vi
    real(dp),                             intent(  out) :: w_fl, w_gr

    w_fl = 0._dp
    w_gr = 0._dp

    select case (C%choice_BMB_subgrid)
      case default
        call crash('unknown choice_BMB_subgrid "' // C%choice_BMB_subgrid // '"')
      case ('FCMP')
        if (geom%mask_floating_ice( vi) .or. geom%mask_gl_fl( vi)) then
          w_fl = 1._dp
        elseif (geom%mask_grounded_ice( vi) .or. geom%mask_gl_gr( vi)) then
          w_gr = 1._dp
        end if
      case ('NMP')
        if (geom%mask_floating_ice( vi) .and. geom%fraction_gr( vi) == 0._dp) then
          w_fl = 1._dp
        elseif (geom%fraction_gr( vi) > 0._dp) then
          w_gr = 1._dp
        end if
      case ('PMP')
        if (geom%mask_floating_ice( vi) .or. geom%mask_grounded_ice( vi)) then
          w_fl = 1._dp - geom%fraction_gr( vi)
          w_gr = geom%fraction_gr( vi)
        end if
    end select

  end subroutine calc_subgrid_BMB_weights

  SUBROUTINE apply_BMB_subgrid_scheme_ROI( mesh, ice, geom, BMB)
    ! Apply selected scheme for sub-grid shelf melt
    ! (see Leguy et al. 2021 for explanations of the three schemes)

    ! In- and output variables
    TYPE(type_mesh),                        INTENT(IN)    :: mesh
    class(atype_ice_model_data),            INTENT(IN)    :: ice
    class(atype_ice_geometry_model_data),   intent(in   ) :: geom
    TYPE(type_BMB_model),                   INTENT(INOUT) :: BMB

    ! Local variables:
    CHARACTER(LEN=256), PARAMETER                         :: routine_name = 'apply_BMB_subgrid_scheme_ROI'
    CHARACTER(LEN=256)                                    :: choice_BMB_subgrid
    INTEGER                                               :: vi

    ! Add routine to path
    CALL init_routine( routine_name)

    ! Note: apply extrapolation_FCMP_to_PMP to non-laddie BMB models before applying sub-grid schemes

    DO vi = mesh%vi1, mesh%vi2
      ! Only for ROI cells
      IF (ice%mask_ROI(vi) > 0) THEN
        CALL compute_subgrid_BMB( geom, BMB, vi)
      END IF
    END DO
    CALL sync

    ! Finalise routine path
    CALL finalise_routine( routine_name)

  END SUBROUTINE apply_BMB_subgrid_scheme_ROI

  subroutine compute_subgrid_BMB( geom, BMB, vi)

    class(atype_ice_geometry_model_data), intent(in   ) :: geom
    type(type_BMB_model),                 intent(inout) :: BMB
    integer             ,                 intent(in   ) :: vi

    ! Determine which sub-grid scheme to apply
    select case (C%choice_BMB_subgrid)
      case default
        call crash('unknown choice_BMB_subgrid "' // C%choice_BMB_subgrid // '"')
      case ('FCMP')
        if (geom%mask_floating_ice( vi) .or. geom%mask_gl_fl( vi)) then
          BMB%BMB( vi) = BMB%BMB_shelf( vi)
        elseif (geom%mask_grounded_ice( vi) .or. geom%mask_gl_gr( vi)) then
          BMB%BMB( vi) = BMB%BMB_sheet( vi)
        else
          BMB%BMB( vi) = 0._dp
        end if
      case ('NMP')
        if (geom%mask_floating_ice( vi) .and. geom%fraction_gr( vi) == 0._dp) then
          BMB%BMB( vi) = BMB%BMB_shelf( vi)
        elseif (geom%fraction_gr( vi) > 0._dp) then
          BMB%BMB( vi) = BMB%BMB_sheet( vi)
        else
          BMB%BMB( vi) = 0._dp
        end if
      case ('PMP')
        if (geom%mask_floating_ice( vi) .or. geom%mask_grounded_ice( vi)) then
          BMB%BMB( vi) = geom%fraction_gr( vi) * BMB%BMB_sheet( vi) + (1._dp - geom%fraction_gr( vi)) * BMB%BMB_shelf( vi)
        else
          BMB%BMB( vi) = 0._dp
        end if
    end select

  end subroutine compute_subgrid_BMB

  subroutine update_laddie_forcing( mesh, ice, geom, ocean, forcing, region_name)

    ! In/output variables
    type(type_mesh),                      intent(in   ) :: mesh
    class(atype_ice_model_data),          intent(in   ) :: ice
    class(atype_ice_geometry_model_data), intent(in   ) :: geom
    type(type_ocean_model),               intent(in   ) :: ocean
    type(type_laddie_forcing),            intent(inout) :: forcing
    character(len=3),                     intent(in   ) :: region_name

    ! Local variables:
    character(len=1024), parameter :: routine_name = 'update_laddie_forcing'
    real(dp)                       :: lambda_M, phi_M, beta_stereo
    integer                        :: vi

    ! Add routine to path
    call init_routine( routine_name)

    forcing%Hi                ( mesh%vi1:mesh%vi2  ) = geom%Hi                ( mesh%vi1:mesh%vi2  )
    forcing%Hs                ( mesh%vi1:mesh%vi2  ) = geom%Hs                ( mesh%vi1:mesh%vi2  )
    forcing%Hb                ( mesh%vi1:mesh%vi2  ) = geom%Hb                ( mesh%vi1:mesh%vi2  )
    forcing%Hib               ( mesh%vi1:mesh%vi2  ) = geom%Hib               ( mesh%vi1:mesh%vi2  )
    forcing%TAF               ( mesh%vi1:mesh%vi2  ) = geom%TAF               ( mesh%vi1:mesh%vi2  )
    forcing%dHib_dx_b         ( mesh%ti1:mesh%ti2  ) = geom%dHib_dx_b         ( mesh%ti1:mesh%ti2  )
    forcing%dHib_dy_b         ( mesh%ti1:mesh%ti2  ) = geom%dHib_dy_b         ( mesh%ti1:mesh%ti2  )
    forcing%mask_icefree_land ( mesh%vi1:mesh%vi2  ) = geom%mask_icefree_land ( mesh%vi1:mesh%vi2  )
    forcing%mask_icefree_ocean( mesh%vi1:mesh%vi2  ) = geom%mask_icefree_ocean( mesh%vi1:mesh%vi2  )
    forcing%mask_grounded_ice ( mesh%vi1:mesh%vi2  ) = geom%mask_grounded_ice ( mesh%vi1:mesh%vi2  )
    forcing%mask_floating_ice ( mesh%vi1:mesh%vi2  ) = geom%mask_floating_ice ( mesh%vi1:mesh%vi2  )

    forcing%mask_gl_fl        ( mesh%vi1:mesh%vi2  ) = geom%mask_gl_fl        ( mesh%vi1:mesh%vi2  )
    forcing%mask_SGD          ( mesh%vi1:mesh%vi2  ) = ice%mask_SGD          ( mesh%vi1:mesh%vi2  )
    forcing%mask              ( mesh%vi1:mesh%vi2  ) = geom%mask              ( mesh%vi1:mesh%vi2  )

    forcing%Ti                ( mesh%vi1:mesh%vi2,:) = ice%Ti                ( mesh%vi1:mesh%vi2,:) - 273.15 ! [degC]
    forcing%T_ocean           ( mesh%vi1:mesh%vi2,:) = ocean%T               ( mesh%vi1:mesh%vi2,:)
    forcing%S_ocean           ( mesh%vi1:mesh%vi2,:) = ocean%S               ( mesh%vi1:mesh%vi2,:)

    ! In case of using PMP, modify forcing masks to treat gl_gr as floating.
    ! The resultant BMB will be multiplied by floating fractions when applying the subgrid scheme
    if (C%choice_BMB_subgrid == 'PMP') then
      do vi = mesh%vi1, mesh%vi2
        if (geom%mask_gl_gr( vi) .and. geom%Hib( vi) < 0._dp) then
          forcing%mask_grounded_ice( vi) = .false.
          forcing%mask_floating_ice( vi) = .true.
        end if
      end do
    end if

    ! Determine which BMB model to run for this region
    select case (region_name)
      case default
        call crash('unknown region_name "' // region_name // '"')
      case ('NAM')
        lambda_M    = C%lambda_M_NAM
        phi_M       = C%phi_M_NAM
        beta_stereo = C%beta_stereo_NAM
      case ('EAS')
        lambda_M    = C%lambda_M_EAS
        phi_M       = C%phi_M_EAS
        beta_stereo = C%beta_stereo_EAS
      case ('GRL')
        lambda_M    = C%lambda_M_GRL
        phi_M       = C%phi_M_GRL
        beta_stereo = C%beta_stereo_GRL
      case ('ANT')
        lambda_M    = C%lambda_M_ANT
        phi_M       = C%phi_M_ANT
        beta_stereo = C%beta_stereo_ANT
    end select

    call calculate_coriolis_parameter( mesh, forcing, lambda_M, phi_M, beta_stereo)

    call checksum( mesh%pai_V  , forcing%Hi                , 'forcing%Hi'                )
    call checksum( mesh%pai_V  , forcing%Hs                , 'forcing%Hs'                )
    call checksum( mesh%pai_V  , forcing%Hb                , 'forcing%Hb'                )
    call checksum( mesh%pai_V  , forcing%Hib               , 'forcing%Hib'               )
    call checksum( mesh%pai_V  , forcing%TAF               , 'forcing%TAF'               )
    call checksum( mesh%pai_Tri, forcing%dHib_dx_b         , 'forcing%dHib_dx_b'         )
    call checksum( mesh%pai_Tri, forcing%dHib_dy_b         , 'forcing%dHib_dy_b'         )
    call checksum( mesh%pai_V  , forcing%mask_icefree_land , 'forcing%mask_icefree_land' )
    call checksum( mesh%pai_V  , forcing%mask_icefree_ocean, 'forcing%mask_icefree_ocean')
    call checksum( mesh%pai_V  , forcing%mask_grounded_ice , 'forcing%mask_grounded_ice' )
    call checksum( mesh%pai_V  , forcing%mask_floating_ice , 'forcing%mask_floating_ice' )
    call checksum( mesh%pai_V  , forcing%mask_gl_fl        , 'forcing%mask_gl_fl'        )
    call checksum( mesh%pai_V  , forcing%mask_SGD          , 'forcing%mask_SGD'          )
    call checksum( mesh%pai_V  , forcing%mask              , 'forcing%mask'              )
    call checksum( mesh%pai_V  , forcing%Ti                , 'forcing%Ti'                )
    call checksum( mesh%pai_V  , forcing%T_ocean           , 'forcing%T_ocean'           )
    call checksum( mesh%pai_V  , forcing%S_ocean           , 'forcing%S_ocean'           )
    call checksum( mesh%pai_Tri, forcing%f_coriolis        , 'forcing%f_coriolis'        )

    ! Finalise routine path
    call finalise_routine( routine_name)

  end subroutine update_laddie_forcing

  subroutine remap_laddie_forcing( mesh_old, mesh_new, forcing)
    ! Reallocate and remap laddie forcing

    ! In- and output variables
    type(type_mesh),                        intent(in)    :: mesh_old
    type(type_mesh),                        intent(in)    :: mesh_new
    type(type_laddie_forcing),              intent(inout) :: forcing

    ! Local variables:
    character(len=256), parameter                         :: routine_name = 'remap_laddie_forcing'

    ! Add routine to path
    call init_routine( routine_name)

    ! Forcing
    call reallocate_dist_shared( forcing%Hi                , forcing%wHi                , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%Hs                , forcing%wHs                , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%Hb                , forcing%wHb                , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%Hib               , forcing%wHib               , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%TAF               , forcing%wTAF               , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%dHib_dx_b         , forcing%wdHib_dx_b         , [mesh_new%pai_Tri%i1_nih, mesh_new%pai_Tri%i2_nih])
    call reallocate_dist_shared( forcing%dHib_dy_b         , forcing%wdHib_dy_b         , [mesh_new%pai_Tri%i1_nih, mesh_new%pai_Tri%i2_nih])
    call reallocate_dist_shared( forcing%mask_icefree_land , forcing%wmask_icefree_land , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask_icefree_ocean, forcing%wmask_icefree_ocean, [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask_grounded_ice , forcing%wmask_grounded_ice , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask_floating_ice , forcing%wmask_floating_ice , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask_gl_fl        , forcing%wmask_gl_fl        , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask_SGD          , forcing%wmask_SGD          , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%mask              , forcing%wmask              , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ])
    call reallocate_dist_shared( forcing%Ti                , forcing%wTi                , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ], [1,mesh_new%nz])
    call reallocate_dist_shared( forcing%T_ocean           , forcing%wT_ocean           , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ], [1,C%nz_ocean])
    call reallocate_dist_shared( forcing%S_ocean           , forcing%wS_ocean           , [mesh_new%pai_V%i1_nih  , mesh_new%pai_V%i2_nih  ], [1,C%nz_ocean])
    call reallocate_dist_shared( forcing%f_coriolis        , forcing%wf_coriolis        , [mesh_new%pai_Tri%i1_nih, mesh_new%pai_Tri%i2_nih])

    ! Finalise routine path
    call finalise_routine( routine_name)

  end subroutine remap_laddie_forcing


  subroutine Frankas_BMB_transition

    ! ! == Total BMB
    ! ! ============

    ! ! Initialise
    ! region%BMB%BMB        = 0._dp
    ! region%BMB%BMB_transition_phase = 0._dp

    ! if (C%do_BMB_transition_phase) then

    !   ! Safety
    !   if (C%BMB_transition_phase_t_start < C%BMB_inversion_t_start .or. C%BMB_transition_phase_t_end > C%BMB_inversion_t_end ) then
    !     ! If the window of smoothing falls outside window of BMB inversion, crash.
    !     call crash(' The time window for BMB smoothing does not fall within the time window for BMB inversion. Make sure that "BMB_transition_phase_t_start" >= "BMB_inversion_t_start", and "BMB_transition_phase_t_end" <= "BMB_inversion_t_end".')

    !   elseif (C%BMB_transition_phase_t_start >= C%BMB_transition_phase_t_end) then
    !     ! If start and end time of smoothing window is equal or start > end, crash.
    !     call crash(' "BMB_transition_phase_t_start" is equivalent or larger than "BMB_transition_phase_t_end".')

    !   end if

    !   ! Compute smoothing weights for BMB inversion smoothing
    !   if (region%time < C%BMB_transition_phase_t_start) then
    !     w = 1.0_dp

    !   elseif (region%time >= C%BMB_transition_phase_t_start .and. &
    !     region%time <= C%BMB_transition_phase_t_end) then
    !     w = 1.0_dp - ((region%time - C%BMB_transition_phase_t_start)/(C%BMB_transition_phase_t_end - C%BMB_transition_phase_t_start))

    !   elseif (region%time > C%BMB_transition_phase_t_end) then
    !     w = 0.0_dp

    !   end if

    ! end if

    ! ! Compute total BMB
    ! do vi = region%mesh%vi1, region%mesh%vi2

    !   ! Skip vertices where BMB does not operate
    !   if (.not. region%geom%mask_gl_gr( vi) .and. &
    !       .not. region%geom%mask_floating_ice( vi) .and. &
    !       .not. region%geom%mask_cf_fl( vi)) cycle

    !   if (C%do_BMB_transition_phase) then
    !     ! If BMB_transition_phase is turned ON, use weight 'w' to compute BMB field
    !     region%BMB%BMB( vi) = w * region%BMB%BMB_inv( vi) + (1.0_dp - w) * region%BMB%BMB_modelled( vi)

    !     ! Save smoothed BMB field for diagnostic output
    !     region%BMB%BMB_transition_phase( vi) = region%BMB%BMB( vi)

    !   else
    !     ! If BMB_transition_phase is turned OFF, just apply inverted melt rates
    !     region%BMB%BMB( vi) = region%BMB%BMB_inv( vi)

    !   end if

    ! end do

  end subroutine Frankas_BMB_transition

END MODULE BMB_main
